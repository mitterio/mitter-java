group "io.mitter"
version "${rootProject.version}"

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }

    ext {
        packageVersions = [
            feign: '10.0.1',
            feignForm: '3.4.0'
        ]
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        Bintray(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId project.name
            version project.version

            pom {
                packaging 'jar'
            }
        }
    }
}

bintray {
    user = properties.property("bintray.user")
    key = properties.property("bintray.key")
    publications = ['Bintray']

    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'mitter'
        licenses = ['Apache-2.0']

        version {
            name = project.version
        }
    }
}

dependencies {
    compile project(':models')
    compile project(':named-entities')

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlinVersion

    compile group: 'io.github.openfeign', name: 'feign-core', version: "${packageVersions.feign}"
    compile group: 'io.github.openfeign', name: 'feign-jackson', version: "${packageVersions.feign}"
    compile group: 'io.github.openfeign', name: 'feign-okhttp', version: "${packageVersions.feign}"
    compile group: 'io.github.microutils', name: 'kotlin-logging', version: '1.4.1'

    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
}
